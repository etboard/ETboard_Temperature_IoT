/******************************************************************************************
 * FileName     : temperature_iot.ino
 * Description  : 이티보드로 온도를 측정하여 스마트폰으로 확인
 * Author       : SCS
 * Created Date : 2022.07.20
 * Reference    : 
 * Modified     : 
 * Modified     : 
******************************************************************************************/

//==========================================================================================
// Include application configuration
//==========================================================================================
#include "app_config.h"
APP_CONFIG app;

//const uint16_t NORMAL_SEND_INTERVAL = 1000 * 5;
#define NORMAL_SEND_INTERVAL  (1000 * 5)

//==========================================================================================
// Include custom header
//==========================================================================================
#include "DHT.h"              // 디지털 온습도 센서
DHT dht(D9, DHT11);           // DHT11

//==========================================================================================
// Declarea Global Variables
//==========================================================================================
float humidity;               // 습도
float temperature;            // 온도

//==========================================================================================
void setup()
//==========================================================================================
{
  app.setup();

  custom_setup();
}

//==========================================================================================
void custom_setup() 
//==========================================================================================
{
  // Insert your custom code here  
}

//==========================================================================================
void loop()
//==========================================================================================
{
  //----------------------------------------------------------------------------------------
  // MQTT loop
  //----------------------------------------------------------------------------------------
  app.mqtt.loop();
    
  //----------------------------------------------------------------------------------------
  //  Send information periodically as NORMAL_SEND_INTERVAL
  //----------------------------------------------------------------------------------------
  if (millis() - app.lastMillis > NORMAL_SEND_INTERVAL) {  
      send_humidity_and_temperature();
      display_Information();
      app.lastMillis = millis();
   }  

  //----------------------------------------------------------------------------------------
  //  Send the dDigital sensor value if changed
  //----------------------------------------------------------------------------------------    
   if (app.mqtt.is_changed_digital() == true) {
      app.mqtt.send_digital();      
   }
  
  //----------------------------------------------------------------------------------------
  // Blink Operation LED
  //----------------------------------------------------------------------------------------  
  app.etboard.normal_blink_led();
}


//==========================================================================================
void onConnectionEstablished()
//==========================================================================================
{
  app.mqtt.onConnectionEstablished();
}

//==========================================================================================
void display_Information() 
//==========================================================================================
{
  String string_t = String(temperature, 2); 
  
  app.oled.setLine(1, app.board_firmware_verion);
  app.oled.setLine(2,"M:" + app.mqtt.mac_address.substring(9));
  app.oled.setLine(3,"T:" + string_t);
  app.oled.display();  
}

//==========================================================================================
void send_humidity_and_temperature()
//==========================================================================================
{ 
  humidity = dht.readHumidity();  
  temperature = dht.readTemperature();  

  if (isnan(humidity) || isnan(temperature)) {
    Serial.println(F("Failed to read from DHT sensor!"));
    return;
  }

  String string_t;
  string_t= String(temperature, 2); 
  app.mqtt.publish("/temperature", String(string_t));

  string_t = String(humidity, 2); 
  app.mqtt.publish("/humidity", String(string_t));
}

//==========================================================================================
//                                                    
// (주)한국공학기술연구원 http://et.ketri.re.kr       
//                                                    
//==========================================================================================
